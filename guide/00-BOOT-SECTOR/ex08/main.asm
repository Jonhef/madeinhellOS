; ------------------------------------------------------------------------------
; Guide:	00-BOOT-SECTOR
; File:		ex08 / main.asm
; Title:	Программа загрузочного сектора, которая входит в 32-битный 
; 			Защищенный Режим
; ------------------------------------------------------------------------------
; Description: 
;	Было бы неплохо остаться в 16-битном реальном режиме, но чтобы использовать
;	возможности процессора полностью, мы должны переключиться в 32-битный
;	защищенный режим.
;	Основные отличия 32-битного защищенного режима:
;		1. Регистры увеличены до 32 бит. Чтобы использовать увеличенные регистры
;		нужно добавить букву "е" перед ними. Пример: AX -> EAX, BX -> EBX
;		2. Добавлены 2 новых регистров сегмента: FS и GS.
;		3. Доступны 32-битные смещения (размер сегмента сможет достигать 4гб)
;		4. Процессор поддерживает более сложную сегментацию памяти, у которой
;		есть 2 достоинства:
;			a. Коду в одном сегменте запрещено исполнять код в другом, более
;			привилигированном сегменте, поэтому вы можете защитить код ядра
;			от кода пользовательских приложений.
;			b. Процессор может предоставлять виртуальную память для процессов
;			пользователя.
;		5. Обработка прерываний также более сложна.
;	Самая сложная часть перехода в 32PM - мы должны подготовить сложную
;	структуру данных, которая называется глобальная таблица дескрипторов (GDT).
; ------------------------------------------------------------------------------


[org 0x7c00]

	mov bp, 0x9000					; Устанавливаем стек
	mov sp, bp

	mov bx, MSG_REAL_MODE
	call print_string				; Печатаем сообщение на экран

	call switch_to_pm				; Переключаемся на загрузочный режим

	jmp $

%include "../ex06/print_string.asm"	; Вывод строки
%include "gdt.asm"					; GDT
%include "print_string_pm.asm"		; Вывод строки в 32 PM
%include "switch.asm"				; Переключиться на 32 PM

[bits 32]

BEGIN_PM:							; Сюда мы попадем после переключения в PM
	mov ebx, MSG_PROT_MODE
	call print_string_pm			; Печатаем сообщение на экран

	jmp $

MSG_REAL_MODE:
	db "Started in 16-bit Real Mode", 0
MSG_PROT_MODE:
	db "Successfully landed in 32-bit Protected mode", 0

times 510-($-$$) db 0
dw 0xaa55
